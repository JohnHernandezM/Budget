Private Sub Worksheet_Change(ByVal Target As Range)
    
    Dim hit_BANK_NAME       As Boolean          ' Add Sheet.
    Dim hit_ACTION          As Boolean          ' Data Validation.
    Dim hit_IN_or_FROM      As Boolean          ' Data Validation.
    
    hit_BANK_NAME = Not Intersect(Target, Me.Range(LBL_rng_BANK_NAME)) Is Nothing
    hit_ACTION = Not Intersect(Target, Me.Range(LBL_rng_ACTION)) Is Nothing
    hit_IN_or_FROM = Not Intersect(Target, Me.Range(LBL_rng_IN_or_FROM)) Is Nothing
    
    If Not (hit_BANK_NAME Or hit_ACTION Or hit_IN_or_FROM) Then Exit Sub
    
    Dim prevEvents          As Boolean
    
    prevEvents = Application.EnableEvents
    Application.EnableEvents = False
    
    On Error GoTo CleanUp

    ' ------------------------------------------------------------------------------------ Watch: Bank -> Add Sheet.
    If hit_BANK_NAME Then
        Dim tbl             As ListObject
        Dim ws              As Worksheet
        Dim lr              As ListRow
        Dim key             As String
        Dim found           As Boolean

        key = Get_SafeText(Me.Range(LBL_rng_BANK_NAME).Value)
        
        Set tbl = Find_Table_ByName(LBL_lo_DATA_BANK_LOGO)
        
        If Not tbl Is Nothing Then
            found = False
            
            If Not tbl.DataBodyRange Is Nothing Then
                Dim keyCol  As Range
                Dim idx     As Variant
                Dim dest    As Range
                Dim srcCell As Range
                
                Set keyCol = tbl.ListColumns(1).DataBodyRange
                
                idx = Application.Match(key, keyCol, 0)
                
                Set dest = Me.Range(LBL_rng_TAB_COLOR)
                
                If Not IsError(idx) Then
                    Set srcCell = tbl.DataBodyRange.Rows(CLng(idx)).Cells(1, 3)
                    
                    On Error Resume Next
                    
                    dest.Interior.Color = srcCell.DisplayFormat.Interior.Color
                    
                    If Err.Number <> 0 Then
                        Err.Clear
                        dest.Interior.Color = srcCell.Interior.Color
                    End If
                    
                    On Error GoTo CleanUp
                    
                    found = True
                End If
            End If
            
            If Not found Then
                Clear_Tab_Color Me.Range(LBL_rng_TAB_COLOR)
            End If
        End If
    End If
    
    ' ------------------------------------------------------------------------------------ Watch: Action & Categories -> DV.
    Dim Input_ACTION        As String
    Dim Input_IN_or_FROM    As String
    
    Input_ACTION = Trim$(CStr(Me.Range(LBL_rng_ACTION).Value))
    Input_IN_or_FROM = Trim$(CStr(Me.Range(LBL_rng_IN_or_FROM).Value))
    
    ' - If ACTION was cleared.
    If hit_ACTION And Len(Input_ACTION) = 0 Then
        Me.Range(LBL_rng_IN_or_FROM).ClearContents
        Clear_DataValidation Me.Range(LBL_rng_ITEM)
        Me.Range(LBL_rng_ITEM).ClearContents
        Remove_Shapes_InCell Me.Range(LBL_rng_BTN_ADD_or_DEL_DV)
        
        GoTo After_COMPLETED
    End If
    
    ' - If IN or FROM was cleared.
    If hit_IN_or_FROM And Len(Input_IN_or_FROM) = 0 Then
        Clear_DataValidation Me.Range(LBL_rng_ITEM)
        Me.Range(LBL_rng_ITEM).ClearContents
        
        GoTo After_COMPLETED
    End If
    
    ' - If ACTION changed.
    If hit_ACTION Then
        Select Case Input_ACTION
            Case LBL_BTN_ADD_DV
                Me.Range(LBL_rng_MODE).Value = "En"
                Me.Range(LBL_rng_IN_or_FROM).ClearContents
                Clear_DataValidation Me.Range(LBL_rng_ITEM)
                Me.Range(LBL_rng_ITEM).ClearContents
                Rebuild_BTN_ADD_or_DEL_DV Me.Range(LBL_rng_BTN_ADD_or_DEL_DV), LBL_BTN_ADD_DV, RGB(25, 107, 36), RGB(25, 107, 36)
                
            Case "Eliminar"
                Me.Range(LBL_rng_MODE).Value = "De"
                Me.Range(LBL_rng_IN_or_FROM).ClearContents
                Clear_DataValidation Me.Range(LBL_rng_ITEM)
                Me.Range(LBL_rng_ITEM).ClearContents
                Rebuild_BTN_ADD_or_DEL_DV Me.Range(LBL_rng_BTN_ADD_or_DEL_DV), "Eliminar", RGB(192, 0, 0), RGB(192, 0, 0)
        End Select
    End If
    
    Input_ACTION = Trim$(CStr(Me.Range(LBL_rng_ACTION).Value))
    Input_IN_or_FROM = Trim$(CStr(Me.Range(LBL_rng_IN_or_FROM).Value))
    
    Dim shp As Shape
    Set shp = Nothing
    
    On Error Resume Next
    
    Set shp = Me.Shapes("BTN_ADD_or_DEL_DV")
    
    On Error GoTo CleanUp
    
    If Input_ACTION = LBL_BTN_ADD_DV Then
        If StrComp(Input_IN_or_FROM, LBL_BTN_ADD_or_DEL_DV_CATEGORY, vbTextCompare) = 0 Then
            If Not shp Is Nothing Then shp.OnAction = "Add_Category"
        ElseIf StrComp(Input_IN_or_FROM, "De", vbTextCompare) = 0 Then
            If Not shp Is Nothing Then shp.OnAction = "Add_De"
        End If
        
    ElseIf Input_ACTION = LBL_BTN_DEL_DV Then
        If StrComp(Input_IN_or_FROM, LBL_BTN_ADD_or_DEL_DV_CATEGORY, vbTextCompare) = 0 Then
            Dim srcCat As Range
            Set srcCat = Get_FirstCol_FromTable(LBL_lo_DATA_BANK_CATEGORY)
            
            If Not srcCat Is Nothing Then
                ApplyListDV Me.Range(LBL_rng_ITEM), srcCat
            End If
            
            If Not shp Is Nothing Then shp.OnAction = "Del_Category"
            
        ElseIf StrComp(Input_IN_or_FROM, LBL_BTN_ADD_or_DEL_DV_DE, vbTextCompare) = 0 Then
            Dim srcDe As Range
            Set srcDe = Get_FirstCol_FromTable(LBL_lo_DATA_BANK_DE)
            
            If Not srcDe Is Nothing Then
                ApplyListDV Me.Range(LBL_rng_ITEM), srcDe
            End If
            
            If Not shp Is Nothing Then shp.OnAction = "Del_De"
        End If
    End If

After_COMPLETED:

CleanUp:
    Application.EnableEvents = prevEvents
    
End Sub


Private Function Get_SafeText(ByVal v As Variant) As String

    If IsError(v) Then
        Get_SafeText = vbNullString
    Else
        Get_SafeText = Trim$(CStr(v))
    End If

End Function

Private Sub Clear_Tab_Color(ByVal rng As Range)

    With rng.Interior
        .Pattern = xlNone
    End With

End Sub

Private Function Find_Table_ByName(ByVal loName As String) As ListObject

    Dim ws As Worksheet
    Dim lo As ListObject
    
    For Each ws In ThisWorkbook.Worksheets
        On Error Resume Next
        
        Set lo = ws.ListObjects(loName)
        
        On Error GoTo 0
        
        If Not lo Is Nothing Then
            Set Find_Table_ByName = lo
            Exit Function
        End If
    Next ws
    
End Function

Private Sub Clear_DataValidation(ByVal rng As Range)

    On Error Resume Next
    
    If rng.Validation.Type <> xlValidateInputOnly Then
        rng.Validation.Delete
    End If
    
    On Error GoTo 0
    
End Sub

Private Sub Remove_Shapes_InCell(ByVal cell As Range)

    Dim i As Long
    
    For i = Me.Shapes.Count To 1 Step -1
        With Me.Shapes(i)
            If Not .TopLeftCell Is Nothing Then
                If .TopLeftCell.Address = cell.Address Then
                    .Delete
                End If
            End If
        End With
    Next i
    
    On Error Resume Next
    
    Me.Shapes("BTN_ADD_or_DEL_DV").Delete
    
    On Error GoTo 0
    
End Sub

Private Function Rebuild_BTN_ADD_or_DEL_DV(ByVal cell As Range, ByVal caption As String, _
                                           ByVal fillRGB As Long, ByVal lineRGB As Long) As Shape
                                           
    Remove_Shapes_InCell cell
    
    Dim shp As Shape
    
    Set shp = Me.Shapes.AddShape(Type:=msoShapeRoundedRectangle, _
                                 Left:=cell.Left, Top:=cell.Top, _
                                 width:=cell.width, height:=cell.height)
    With shp
        .Name = "BTN_ADD_or_DEL_DV"
        .TextFrame2.TextRange.Characters.Text = caption
        
        With .TextFrame2.TextRange.Font
            .Bold = msoTrue
            .Size = 10
            .Fill.ForeColor.RGB = RGB(255, 255, 255)
        End With
        
        .TextFrame2.VerticalAnchor = msoAnchorMiddle
        .TextFrame2.TextRange.ParagraphFormat.Alignment = msoAlignCenter
        
        .Fill.ForeColor.RGB = fillRGB
        .Line.ForeColor.RGB = lineRGB
    End With
    
    Set Rebuild_BTN_ADD_or_DEL_DV = shp
    
End Function


Private Function Get_FirstCol_FromTable(ByVal loName As String) As Range

    Dim lo As ListObject
    Set lo = Find_Table_ByName(loName)
    
    If Not lo Is Nothing Then
        If Not lo.DataBodyRange Is Nothing Then
            Set Get_FirstCol_FromTable = lo.ListColumns(1).DataBodyRange
        End If
    End If
    
End Function

Private Sub ApplyListDV(ByVal Target As Range, ByVal src As Range)

    Dim addr As String
    
    addr = "=" & src.Address(RowAbsolute:=True, ColumnAbsolute:=True, ReferenceStyle:=xlA1, External:=True)
    Clear_DataValidation Target
    Target.Validation.Add Type:=xlValidateList, AlertStyle:=xlValidAlertStop, Operator:=xlBetween, Formula1:=addr
    
End Sub
