Option Explicit

' ==============================================================================================================================================
' ------------------------------------------------------------- ADD SHEET: Button Installer ----------------------------------------------------
' ==============================================================================================================================================

' - One-time installer.
Public Sub Install_BTN_AddSheet()

    Dim ws As Worksheet, shp As Shape, topLeft As Range
    
    Set ws = ThisWorkbook.Worksheets("+")
    Set topLeft = ws.Range("B8")
    
    On Error Resume Next
    
    ws.Shapes("btnAddSheet").Delete
    
    On Error GoTo 0

    Set shp = ws.Shapes.AddShape(msoShapeRoundedRectangle, topLeft.Left, topLeft.Top, ws.Columns("B").width, topLeft.RowHeight)
    
    With shp
        .Name = "btnAddSheet"
        .TextFrame2.TextRange.Text = "Add Sheet"
        .TextFrame2.TextRange.Font.Bold = msoTrue
        .TextFrame2.TextRange.Font.Size = 10
        .TextFrame2.TextRange.Font.Fill.ForeColor.RGB = RGB(255, 255, 255)
        .TextFrame2.VerticalAnchor = msoAnchorMiddle
        .TextFrame2.TextRange.ParagraphFormat.Alignment = msoAlignCenter

        With .Fill
            .Visible = msoTrue
            .ForeColor.RGB = RGB(25, 107, 36)
            .Solid
        End With
        
        With .Line
            .Visible = msoTrue
            .ForeColor.RGB = RGB(25, 107, 36)
        End With
        
        .Adjustments.Item(1) = 0.25
        .OnAction = "Add_Sheet"
        .Placement = xlFreeFloating
    End With
    
End Sub



' ==============================================================================================================================================
' ------------------------------------------------------------- ADD SHEET: Button Handler ------------------------------------------------------
' ==============================================================================================================================================

' - Add Sheet Button handler.
Public Sub Add_Sheet()

    Dim wsPlus As Worksheet, wsNew As Worksheet
    Dim newName As String
    Dim LogoFinder As String

    Set wsPlus = ThisWorkbook.Worksheets("+")
    
    newName = UCase(Trim$(CStr(wsPlus.Range("C4").Value)))

    If Len(newName) = 0 Then
        MsgBox "Celda C4 vacía.", vbExclamation
        Exit Sub
    End If

    If Check_Sheet_Exists(newName) Then
        MsgBox "Una Hoja llamada '" & newName & "' ya existe.", vbCritical
        wsPlus.Range("C4").ClearContents
        Exit Sub
    End If

    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    On Error GoTo CleanFail

    Set wsNew = ThisWorkbook.Worksheets.Add(After:=ThisWorkbook.Worksheets(ThisWorkbook.Worksheets.Count))
    
    wsNew.Name = newName

    Call Set_MainTable(wsNew)

    Build_New_Sheet wsNew

    On Error Resume Next
    
    Install_InSheet = newName
    Application.Run "Install_BTN_AddRow"
    Application.Run "Install_BTN_DelRow"
    
    Inject_Code wsNew
    
    LogoFinder = "=VLOOKUP(" & Chr(34) & wsPlus.Range("C2") & Chr(34) & ", Data_Bank_Logo, 2, FALSE)"
    wsNew.Range("B2").Formula = LogoFinder

    wsNew.Tab.Color = wsPlus.Range("C6").Interior.Color
    
    wsPlus.Range("C2").ClearContents
    wsPlus.Range("C4").ClearContents
    wsPlus.Range("C6").Interior.Color = RGB(255, 255, 255)
    
    On Error GoTo 0

CleanExit:
    Application.EnableEvents = True
    Application.ScreenUpdating = True
    Exit Sub

CleanFail:
    MsgBox "Error while creating the sheet: " & Err.Description, vbCritical, "Add Sheet"
    Resume CleanExit
    
End Sub



' ==============================================================================================================================================
' ------------------------------------------------------------- ADD SHEET: Button Helpers ------------------------------------------------------
' ==============================================================================================================================================

' - Builds the New Sheet.
Public Sub Build_New_Sheet(ByVal ws As Worksheet)

    Dim loMain As ListObject, lo2025 As ListObject, lo2026 As ListObject
    Dim prevSheet As Worksheet
    Dim rng As Range
    
    With ws
        .Range("B2:AJ2").Merge
        .Range("B4:J4").Merge
        .Range("L4:W4").Merge
        .Range("Y4:AJ4").Merge

        With .Range("B4:J4")
            .Value = "C O N S U M O S"
            .HorizontalAlignment = xlCenter
            .VerticalAlignment = xlCenter
            .Interior.Color = RGB(R_Title_Fill, G_Title_Fill, B_Title_Fill)
            .Font.Color = RGB(R_Title_Font, G_Title_Font, B_Title_Font)
            .Font.Bold = True
        End With

        With .Range("L4:W4")
            .Value = "2 0 2 5"
            .HorizontalAlignment = xlCenter
            .VerticalAlignment = xlCenter
            .Interior.Color = RGB(R_Title_Fill, G_Title_Fill, B_Title_Fill)
            .Font.Color = RGB(R_Title_Font, G_Title_Font, B_Title_Font)
            .Font.Bold = True
        End With

        With .Range("Y4:AJ4")
            .Value = "2 0 2 6"
            .HorizontalAlignment = xlCenter
            .VerticalAlignment = xlCenter
            .Interior.Color = RGB(R_Title_Fill, G_Title_Fill, B_Title_Fill)
            .Font.Color = RGB(R_Title_Font, G_Title_Font, B_Title_Font)
            .Font.Bold = True
        End With
    End With

    ws.Range("B6").Resize(1, 9).Value = Array("FECHA", "DE", "CATEGORÍA", "DESCRIPCIÓN", "MONTO", "CUOTAS", "MONTO CUOTA", "[ M ]", "[ A ]")

    Set loMain = Add_Table(ws, "B6:J7", Get_Name_MainTable(), "TableStyleMedium5")

    With ws.Range("B6:J6")
        .Interior.Color = RGB(R_Header_Fill, G_Header_Fill, B_Header_Fill)
        .Font.Color = vbWhite
        .Font.Bold = True
    End With
    
    On Error Resume Next
    
    loMain.ShowAutoFilter = False
    
    On Error GoTo 0

    ws.Range("B7").NumberFormat = "[$-en-US]d - mmm - yy;@"
    ws.Range("F7,H7").NumberFormat = "$ #,##0"

    ws.Range("I6:J6").Interior.Color = RGB(R_IJ_Header_Fill, G_IJ_Header_Fill, B_IJ_Header_Fill)
    ws.Range("I7:J7").Interior.Color = RGB(R_IJ_FirstRow_Fill, G_IJ_FirstRow_Fill, B_IJ_FirstRow_Fill)

    Apply_DataValidation ws.Range("C7"), "Data_Bank_De"
    Apply_DataValidation ws.Range("D7"), "Data_Bank_Category"
    Apply_DataValidation ws.Range("I7"), "Data_Bank_Month"
    Apply_DataValidation ws.Range("J7"), "Data_Bank_Year"

    ws.Range("L6").Resize(1, 12).Value = Array("ENE", "FEB", "MAR", "ABR", "MAY", "JUN", "JUL", "AGO", "SEP", "OCT", "NOV", "DIC")
    
    Set lo2025 = Add_Table(ws, "L6:W7", Get_Name_MainTable() & "_2025", "TableStyleMedium5")
    
    lo2025.ShowAutoFilter = False
    
    With ws.Range("L6:W6")
        .Interior.Color = RGB(R_Header_Fill, G_Header_Fill, B_Header_Fill)
        .Font.Color = vbWhite
        .Font.Bold = True
    End With

    ws.Range("Y6").Resize(1, 12).Value = ws.Range("L6").Resize(1, 12).Value
    
    Set lo2026 = Add_Table(ws, "Y6:AJ7", Get_Name_MainTable() & "_2026", "TableStyleMedium5")
    
    lo2026.ShowAutoFilter = False
    
    With ws.Range("Y6:AJ6")
        .Interior.Color = RGB(R_Header_Fill, G_Header_Fill, B_Header_Fill)
        .Font.Color = vbWhite
        .Font.Bold = True
    End With

    Call Apply_Layout_From_Table(ws, "Data", "Data_Bank_Layout")

    ws.Rows("11:" & ws.Rows.Count).EntireRow.Hidden = True
    ws.Range("AL1:XFD1").EntireColumn.Hidden = True
    
    ws.Cells.VerticalAlignment = xlCenter
    
    loMain.Range.IndentLevel = 1
    lo2025.Range.HorizontalAlignment = xlCenter
    lo2026.Range.HorizontalAlignment = xlCenter
    
    ws.Range("I:J").HorizontalAlignment = xlCenter
    
    Set prevSheet = ActiveSheet
    
    ws.Activate
    ActiveWindow.DisplayGridlines = False
    prevSheet.Activate
    
End Sub


' - Gets Main Table Name.
Public Function Get_Name_MainTable() As String

    On Error Resume Next
    If Len(MainTable) = 0 Then MainTable = "MainTable"
    Get_Name_MainTable = MainTable
    
End Function


' - Creates New Tables.
Public Function Add_Table(ByVal ws As Worksheet, ByVal a1Range As String, ByVal tableName As String, ByVal tableStyle As String) As ListObject

    Dim lo As ListObject
    
    If ws.Range(a1Range).Rows.Count < 2 Then
        Dim r As Range
        Set r = ws.Range(a1Range)
        Set r = r.Resize(2, r.Columns.Count)
        Set Add_Table = ws.ListObjects.Add(xlSrcRange, r, , xlYes)
    Else
        Set Add_Table = ws.ListObjects.Add(xlSrcRange, ws.Range(a1Range), , xlYes)
    End If

    Set lo = Add_Table
    
    On Error Resume Next
    
    lo.Name = tableName
    
    On Error GoTo 0
    
    lo.tableStyle = tableStyle
    
End Function


' - Applies Data Validation to first Column.
Public Sub Apply_DataValidation(ByVal Target As Range, ByVal tableName As String)

    Dim lo As ListObject
    Dim srcRng As Range
    On Error GoTo FailDV

    Set lo = ThisWorkbook.Worksheets("Data").ListObjects(tableName)

    If Not lo.DataBodyRange Is Nothing Then
        Set srcRng = lo.ListColumns(1).DataBodyRange
    Else
        Set srcRng = lo.ListColumns(1).Range
        
        If srcRng.Rows.Count > 1 Then
            Set srcRng = srcRng.offset(1).Resize(srcRng.Rows.Count - 1)
        End If
    End If

    With Target.Validation
        .Delete
        .Add Type:=xlValidateList, AlertStyle:=xlValidAlertStop, Operator:=xlBetween, Formula1:="=" & srcRng.Address(True, True, xlA1, True)
        .IgnoreBlank = True
        .InCellDropdown = True
        .ShowInput = True
        .ShowError = True
    End With
    
    Exit Sub

FailDV:
    On Error Resume Next
    Target.Validation.Delete
    On Error GoTo 0
    
End Sub


' - Checks if a Sheets already exists with the input Name.
Public Function Check_Sheet_Exists(ByVal sheetName As String) As Boolean

    Dim ws As Worksheet
    On Error Resume Next
    Set ws = ThisWorkbook.Worksheets(sheetName)
    Check_Sheet_Exists = Not ws Is Nothing
    On Error GoTo 0
    
End Function


' - Reads from Data Sheet.
' - Table: Data_Bank_Layout.
Public Function Apply_Layout_From_Table(ByVal wsTarget As Worksheet, Optional ByVal layoutSheetName As String = "Data", Optional ByVal tableName As String = "Data_Bank_Layout") As Boolean

    On Error GoTo EH

    Dim lo As ListObject
    Dim r As ListRow
    Dim setting As String, selector As String
    Dim v As Variant, valNum As Double

    Set lo = ThisWorkbook.Worksheets(layoutSheetName).ListObjects(tableName)

    For Each r In lo.ListRows
        setting = Trim(CStr(r.Range.Cells(1, 1).Value2))
        selector = Trim(CStr(r.Range.Cells(1, 2).Value2))
        v = r.Range.Cells(1, 3).Value2

        If Len(selector) > 0 And Not IsEmpty(v) Then
            If IsNumeric(v) Then
                valNum = CDbl(v)
            Else
                valNum = Val(CStr(v))
            End If

            Select Case LCase$(setting)
                Case "row"
                    Apply_To_Rows wsTarget, selector, valNum

                Case "column"
                    Apply_To_Columns wsTarget, selector, valNum
            End Select
        End If
    Next r

    Apply_Layout_From_Table = True
    
    Exit Function
EH:
    Apply_Layout_From_Table = False
    
End Function


' - Applies settings to Rows.
Public Sub Apply_To_Rows(ByVal ws As Worksheet, ByVal selector As String, ByVal height As Double)

    Dim parts() As String, token As Variant, s As String
    
    parts = Split(selector, ",")

    For Each token In parts
        s = Trim(CStr(token))
        If Len(s) = 0 Then GoTo NextToken

        If InStr(1, s, ":", vbTextCompare) > 0 Then
            ws.Rows(s).RowHeight = height
        ElseIf IsNumeric(s) Then
            ws.Rows(CLng(s)).RowHeight = height
        Else
            ' Ignore
        End If
        
NextToken:
    Next token
    
End Sub


' - Applies settings to Columns.
Public Sub Apply_To_Columns(ByVal ws As Worksheet, ByVal selector As String, ByVal width As Double)

    Dim parts() As String, token As Variant, s As String
    parts = Split(selector, ",")

    For Each token In parts
        s = Trim(CStr(token))
        If Len(s) = 0 Then GoTo NextToken
        ws.Columns(s).ColumnWidth = width
        
NextToken:
    Next token
    
End Sub



' ==============================================================================================================================================
' ------------------------------------------------------------- ADD SHEET: Code Injection ------------------------------------------------------
' ==============================================================================================================================================

' - Injects Code into new Sheets.
Public Sub Inject_Code(ByVal ws As Worksheet)

    Const vbext_pk_Proc As Long = 0

    Dim vbProj As Object
    Dim vbComp As Object
    Dim cm As Object
    
    Dim WS_A_Procedure As String
    Dim WS_A_CodeLines As String
    Dim WS_A_StartLine As Long
    Dim WS_A_NumLines As Long
    
    Dim WS_SC_Procedure As String
    Dim WS_SC_CodeLines As String
    Dim WS_SC_StartLine As Long
    Dim WS_SC_NumLines As Long
    
    Dim WS_C_Procedure As String
    Dim WS_C_CodeLines As String
    Dim WS_C_StartLine As Long
    Dim WS_C_NumLines As Long
    
    On Error GoTo Fail

    Set vbProj = ThisWorkbook.VBProject
    Set vbComp = vbProj.VBComponents(ws.CodeName)
    Set cm = vbComp.CodeModule

    WS_A_Procedure = "Worksheet_Activate"
    WS_SC_Procedure = "Worksheet_SelectionChange"
    WS_C_Procedure = "Worksheet_Change"
    
    On Error Resume Next
    
    WS_A_StartLine = cm.ProcWS_A_StartLine(WS_A_StartLine, vbext_pk_Proc)
    WS_SC_StartLine = cm.ProcWS_SC_StartLine(WS_SC_Procedure, vbext_pk_Proc)
    WS_C_StartLine = cm.ProcWS_C_StartLine(WS_C_StartLine, vbext_pk_Proc)
    
    If WS_A_StartLine > 0 Then
        WS_A_NumLines = cm.ProcCountLines(WS_A_Procedure, vbext_pk_Proc)
        cm.DeleteLines WS_A_StartLine, WS_A_NumLines
    End If
    
    If WS_SC_StartLine > 0 Then
        WS_SC_NumLines = cm.ProcCountLines(WS_SC_Procedure, vbext_pk_Proc)
        cm.DeleteLines WS_SC_StartLine, WS_SC_NumLines
    End If
    
    If WS_C_StartLine > 0 Then
        WS_C_NumLines = cm.ProcCountLines(WS_C_Procedure, vbext_pk_Proc)
        cm.DeleteLines WS_C_StartLine, WS_C_NumLines
    End If
    
    On Error GoTo Fail

    WS_A_CodeLines = _
        "Option Explicit" & vbCrLf & vbCrLf & _
        "' INITIALIZE" & vbCrLf & _
        "Private Sub Worksheet_Activate()" & vbCrLf & _
        "    Initialize_WS_A Me" & vbCrLf & _
        "End Sub" & vbCrLf
    
    WS_SC_CodeLines = _
        "' SYNC DELETIONS WHEN SELECTION CHANGES" & vbCrLf & _
        "Private Sub Worksheet_SelectionChange(ByVal Target As Range)" & vbCrLf & _
        "    Sync_WS_SC Me, Target" & vbCrLf & _
        "End Sub" & vbCrLf
        
    WS_C_CodeLines = _
        "' PROCESS MAIN EVENT" & vbCrLf & _
        "Private Sub Worksheet_Change(ByVal Target As Range)" & vbCrLf & _
        "    Process_WS_C Me, Target" & vbCrLf & _
        "End Sub"
    
    cm.InsertLines cm.CountOfLines + 1, WS_A_CodeLines
    cm.InsertLines cm.CountOfLines + 1, WS_SC_CodeLines
    cm.InsertLines cm.CountOfLines + 1, WS_C_CodeLines
    
    Exit Sub

Fail:
    MsgBox "Could not inject SelectionChange handler on sheet '" & ws.Name & "'." & vbCrLf & _
           "• Ensure the workbook is .xlsm" & vbCrLf & _
           "• Enable: File > Options > Trust Center > Trust Center Settings > 'Trust access to the VBA project object model'", _
           vbCritical

End Sub
