Option Explicit

' ==============================================================================================================================================
' --------------------------------------------------------------------- + ----------------------------------------------------------------------
' ==============================================================================================================================================

' - Adds Banking Sheet.
Private Sub Worksheet_Change(ByVal Target As Range)

    On Error GoTo CleanUp

    If Intersect(Target, Me.Range(LBL_rng_BANK_NAME)) Is Nothing Then Exit Sub

    Dim prevEvents As Boolean
    prevEvents = Application.EnableEvents
    
    Application.EnableEvents = False

    Dim key As String
    key = Trim$(CStr(Me.Range(LBL_rng_BANK_NAME).Value))

    If Len(key) = 0 Then
        Me.Range(LBL_rng_TAB_COLOR).Interior.ColorIndex = xlColorIndexNone
        GoTo CleanUp
    End If

    Dim tbl As ListObject
    Set tbl = Find_BankTable(LBL_lo_DATA_BANK_LOGO)

    If tbl Is Nothing Then
        Me.Range(LBL_rng_TAB_COLOR).Interior.ColorIndex = xlColorIndexNone
        GoTo CleanUp
    End If

    If tbl.ListRows.Count = 0 Then
        Me.Range(LBL_rng_TAB_COLOR).Interior.ColorIndex = xlColorIndexNone
        GoTo CleanUp
    End If

    Dim firstCol As Range
    Set firstCol = tbl.ListColumns(col_DATA_BANK_LOGO_NAME).DataBodyRange

    Dim hit As Range
    Set hit = firstCol.Find(What:=key, LookIn:=xlValues, LookAt:=xlWhole, _
                            SearchOrder:=xlByRows, SearchDirection:=xlNext, MatchCase:=False)

    With Me.Range(LBL_rng_TAB_COLOR).Interior
        If Not hit Is Nothing Then
            .Color = hit.Offset(0, col_DATA_BANK_LOGO_TAB - col_DATA_BANK_LOGO_NAME).Interior.Color
        Else
            .ColorIndex = xlColorIndexNone
        End If
    End With

CleanUp:
    Application.EnableEvents = prevEvents
    
End Sub
