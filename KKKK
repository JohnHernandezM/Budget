==================================== SCRIPT COLUMN WIDTH ====================================

function main(workbook: ExcelScript.Workbook) {
  const sheet = workbook.getActiveWorksheet();
  if (!sheet) return;

  let col = 0; // A = 0
  let usedCols = 0;

  while (true) {
    const cell = sheet.getCell(0, col); // Row 1, current column
    const v = cell.getValue();

    // Stop at first blank
    if (v === "" || v === null || v === undefined) break;

    // Coerce to number if it's text like "12"
    const width = typeof v === "number" ? v : Number(v);
    if (!Number.isNaN(width) && Number.isFinite(width)) {
      sheet.getRangeByIndexes(0, col, 1, 1).getColumn().setWidth(width);
    }

    usedCols = col + 1; // track how far we went
    col++;
  }

  // Clear only the used part of row 1 (e.g., A1:E1)
  if (usedCols > 0) {
    sheet.getRangeByIndexes(0, 0, 1, usedCols)
         .clear(ExcelScript.ClearApplyTo.contents);
  }
}


=============================================================================================


1. Paste the initial Policy Data in Table 1, and start working on Table 2. 
2. [L]: Void   —  Enter all endorsements that need to be voided, separating each item with a comma.
3. [M]: TRN  —  Run the "WC – Clean-Up – TRN" script. (Navigate to Automate > All Scripts > Run Script, or simply click RUN!)

*** TRN ***

=IF(AND(M9<>"", $CD$2="PASS"),
  LET(
  parts, TEXTSPLIT(TRIM(M9), ",", , TRUE),
  tok,   FILTER(TRIM(parts), TRIM(parts)<>""),
  desc,  SORTBY(tok, VALUE(tok), -1),
  TRANSPOSE(desc) & ":"
  ),
"")

*** Include in BO/RI ***
Data Validation: 
=IF(N9<>"", $CB$2:$CB$3, $CB$4:$CB$4)

*** Day 1 ***
=IF(N9<>"",LET(
  tok,TEXTBEFORE(N9#,":"),
  n,ROWS(tok),
  pRange,P9:INDEX(P:P,ROW(P9)+n-1),
  pNorm,UPPER(TRIM(pRange)),
  IF(COUNTBLANK(pRange)>0,"",
    LET(
      yesList,IFERROR(FILTER(tok,pNorm="YES"),""),
      msg,IF(COUNTA(yesList)=0,
              "Process BO/RI.",
              "Process BO/RI."&CHAR(10)&TEXTJOIN(CHAR(10),TRUE,
                 "Include changes from Transaction "&SORT(yesList,1,1)&" in the BO/RI.")
      ),
      revTok,INDEX(tok,SEQUENCE(n,,n,-1)),
      revPNorm,INDEX(pNorm,SEQUENCE(n,,n,-1)),
      rightMask,revPNorm<>"YES",
      rowsRight,SUM(--rightMask),
      rightTok,IF(rowsRight=0,"",INDEX(revTok,FILTER(SEQUENCE(n),rightMask))),
      total,2*(2*n+1),
      seq,SEQUENCE(1,total),
      IF(ISODD(seq),
        LET(k,(seq+1)/2,
          IF(k=n+1,
             msg,
             IF(k<=n,
                "Void Transaction "&INDEX(tok,k)&".",
                LET(r,k-(n+1),
                  IF(r<=rowsRight,
                     LET(t,INDEX(rightTok,r),
                         IF(t="","", "Void Transaction "&t&".")
                     ),
                     ""
                  )
                )
             )
          )
        ),
        ""
      )
    )
  )
),"")

=============================================================== HEADERS ============================================================

*** Days ***
=LET(
  rowStart, 8,
  lastRow, IFERROR(LOOKUP(2,1/(J:J<>""),ROW(J:J)), 0),
  rowEnd, MAX(rowStart, lastRow),
  rng, INDEX(J:J, rowStart):INDEX(J:J, rowEnd),
  hasData, COUNTA(rng)>0,
  IF(
    hasData,
    LET(
      maxN, MAX(rng),
      nums, maxN*2 + 1,
      total, 2*nums,
      seq, SEQUENCE(1, total),
      IF(ISODD(seq), "Day " & (seq+1)/2, "")
    ),
    ""
  )
)


*** Action/Done ***

=LET(
  rowStart, 4,
  rowEnd, MAX(rowStart, LOOKUP(2, 1/(K:K<>""), ROW(K:K))),
  rng, INDEX(K:K, rowStart):INDEX(K:K, rowEnd),
  maxN, MAX(rng),
  nums, maxN*2 + 1,
  total, 2*nums,
  seq, SEQUENCE(1, total),
  IF(ISODD(seq), "Action", "Done")
)


=LET(
  rowStart, 9,
  lastRow, IFERROR(LOOKUP(2,1/(K:K<>""),ROW(K:K)), 0),
  rowEnd, MAX(rowStart, lastRow),
  rng, INDEX(K:K, rowStart):INDEX(K:K, rowEnd),
  hasData, COUNTA(rng)>0,
  IF(
    hasData,
    LET(
      maxN, MAX(rng),
      nums, maxN*2 + 1,
      total, 2*nums,
      seq, SEQUENCE(1, total),
      IF(ISODD(seq), "Action", "Done")
    ),
    ""
  )
)
