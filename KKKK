VOID TRN NBR

=LET(
  parts, TRIM(TEXTSPLIT(M13, "," , , TRUE)),
  x, FILTER(parts, parts<>""),                         
  n, COLUMNS(x),
  seq, SEQUENCE(1, 2*n-1),
  IF(ISODD(seq), INDEX(x, (seq+1)/2), "")
)


-------------------------------------------------------------------------------------------------

VOID TRN NBR INCLUDING BORI

=LET(
  parts, TEXTSPLIT(TRIM(M11), ",",,TRUE),
  x, FILTER(TRIM(parts), TRIM(parts)<>""),     
  n, COLUMNS(x),
  total, 2*(2*n+1),                              
  seq, SEQUENCE(1, total),
  IF(ISODD(seq),
     LET(k, (seq+1)/2,                           
        IF(k = n+1, "Process BO/RI",
           IF(k <= n, INDEX(x, k),
                     INDEX(x, 2*n+2 - k))
        )
     ),
     ""
  )
)


-------------------------------------------------------------------------------------------------

CREATE DAYS

=LET(
  last, LOOKUP(2,1/(M:M<>""),ROW(M:M)),
  rng, M2:INDEX(M:M,last),
  counts, BYROW(rng, LAMBDA(x,
    LET(arr, TEXTSPLIT(TRIM(x), ",",,TRUE), IFERROR(COLUMNS(FILTER(arr, arr<>"")),0))
  )),
  maxN, MAX(counts),
  nums, 2*maxN+1,                 
  total, 2*nums,                
  seq, SEQUENCE(1,total),
  IF(ISODD(seq), (seq+1)/2, "")
)


-------------------------------------------------------------------------------------------------

COLUMN N:

=LET(
  parts, TEXTSPLIT(TRIM(M11), ",",,TRUE),
  tok,   FILTER(TRIM(parts), TRIM(parts)<>""),  
  TRANSPOSE(tok) & ":"
)


-------------------------------------------------------------------------------------------------

COLUMN P:

=IF(N11#<>"", IF(O11:INDEX(O:O, ROW(O11)+ROWS(N11#)-1),"☑","☐"), "")


=LET(
  h, ROWS(N11#),
  sel, O11:INDEX(O:O, ROW(O11)+h-1),
  IF(N11#<>"",
     IF(sel="","", IF(sel="Yes","☑","☐")),
     ""
  )
)


-------------------------------------------------------------------------------------------------

BORI WITH TOKENS:

=LET(
  tok, TEXTBEFORE(N11#,":"),                      
  n, ROWS(tok),
  pRange, P11:INDEX(P:P, ROW(P11)+n-1),              
  pNorm, UPPER(TRIM(pRange)),

  yesList, IFERROR(FILTER(tok, pNorm="YES"), ""),
  msg, IF(COUNTA(yesList)=0,
          "Process BO/RI",
          "Process BO/RI"&CHAR(10)&TEXTJOIN(CHAR(10), TRUE,
             "Include changes from Transaction "&SORT(yesList,1,1)&" in the BO/RI.")
  ),

  revTok, INDEX(tok, SEQUENCE(n,,n,-1)),
  revPNorm, INDEX(pNorm, SEQUENCE(n,,n,-1)),
  rightArr, IFERROR(FILTER(revTok, revPNorm<>"YES"), ""),
  rowsRight, IFERROR(ROWS(rightArr),0),

  total, 2*(2*n+1),                        
  seq, SEQUENCE(1,total),

  IF(ISODD(seq),
    LET(k,(seq+1)/2,
      IF(k=n+1, msg,
        IF(k<=n, INDEX(tok,k),
          LET(r, k-(n+1),
            IF(r<=rowsRight, INDEX(rightArr, r), "")
          )
        )
      )
    ),
    ""
  )
)


BORI WITH TOKENS AND VOID TRANSACTION:

=LET(
  tok, TEXTBEFORE(N11#,":"),                        
  n, ROWS(tok),
  pRange, P11:INDEX(P:P, ROW(P11)+n-1),              
  pNorm, UPPER(TRIM(pRange)),

  yesList, IFERROR(FILTER(tok, pNorm="YES"), ""),
  msg, IF(COUNTA(yesList)=0,
          "Process BO/RI.",
          "Process BO/RI."&CHAR(10)&TEXTJOIN(CHAR(10), TRUE,
             "Include changes from Transaction "&SORT(yesList,1,1)&" in the BO/RI.")
  ),

  revTok,   INDEX(tok, SEQUENCE(n,,n,-1)),
  revPNorm, INDEX(pNorm, SEQUENCE(n,,n,-1)),
  rightMask, revPNorm<>"YES",
  rowsRight, SUM(--rightMask),
  rightTok, IF(rowsRight=0, "", INDEX(revTok, FILTER(SEQUENCE(n), rightMask))),

  total, 2*(2*n+1),                                  
  seq, SEQUENCE(1,total),

  IF(ISODD(seq),
    LET(k,(seq+1)/2,
      IF(k=n+1,
         msg,
         IF(k<=n,
            "Void Transaction "&INDEX(tok,k)&".",
            LET(r, k-(n+1),
              IF(r<=rowsRight,
                 LET(t, INDEX(rightTok, r),
                     IF(t="","", "Void Transaction "&t&".")
                 ),
                 ""
              )
            )
         )
      )
    ),
    ""
  )
)

