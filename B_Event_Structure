Option Explicit

' ==============================================================================================================================================
' -------------------------------------------------- 1. INITIALIZE: Worksheet Activate ---------------------------------------------------------
' ==============================================================================================================================================

Public Sub Initialize_WS_A(ByVal ws As Worksheet)

    Dim prevEvents As Boolean
    prevEvents = Application.EnableEvents
    
    On Error GoTo CleanExit
    Application.EnableEvents = False
    
    Set_MainTable ws

    MONTHS = Array("Ene", "Feb", "Mar", "Abr", "May", "Jun", "Jul", "Ago", "Sep", "Oct", "Nov", "Dic")
    
    SIG_DELIM = ChrW(&H241F)

    Place_AddButton_AtRow 9, ws

    Refresh_Snapshot ws, MainTable
    
    lastKnownListRowsCount = prevMainCount

    Set_Visibility_ForSheet ws, MainTable
    
    Update_Protection_BANK ws

CleanExit:
    Application.EnableEvents = prevEvents
    Update_Protection_BANK ws

End Sub



' ==============================================================================================================================================
' -------------------------------------------------- 2. DELETION SYNC: Worksheet SelectionChange -----------------------------------------------
' ==============================================================================================================================================

Public Sub Sync_WS_SC(ByVal ws As Worksheet, ByVal Target As Range)

    Dim prevEvents As Boolean, prevScreen As Boolean
    
    prevEvents = Application.EnableEvents
    prevScreen = Application.ScreenUpdating

    On Error GoTo CleanExit
    
    Application.EnableEvents = False
    Application.ScreenUpdating = False

    Dim lo As ListObject
    Set lo = Nothing
    
    On Error Resume Next
    
    Set_MainTable ws
        
    Set lo = ws.ListObjects(MainTable)
    
    On Error GoTo 0

    Dim needSync As Boolean: needSync = False
    
    If Not lo Is Nothing Then
        Dim currCount As Long
        currCount = IIf(lo.DataBodyRange Is Nothing, 0, lo.ListRows.Count)

        If Not Target Is Nothing Then
            If Not lo.DataBodyRange Is Nothing Then
                If Not Intersect(Target, lo.DataBodyRange) Is Nothing Then needSync = True
            End If
        End If

        If currCount <> lastKnownListRowsCount Then
            needSync = True
            lastKnownListRowsCount = currCount
        End If
    End If

    If needSync Then
        Sync_Deletion_MainTable_To_YearTables ws
    End If

    Check_AddButton_Position ws, ws.ListObjects(MainTable)
    Set_Visibility_ForSheet ws, MainTable

CleanExit:
    Application.ScreenUpdating = prevScreen
    Application.EnableEvents = prevEvents

End Sub



' ==============================================================================================================================================
' -------------------------------------------------- 3. PROCESS EVENT: Worksheet Change --------------------------------------------------------
' ==============================================================================================================================================

Public Sub Process_WS_C(ByVal ws As Worksheet, ByVal Target As Range)

    Dim prevEvents As Boolean, prevScreen As Boolean, prevCalc As XlCalculation
    
    prevEvents = Application.EnableEvents
    prevScreen = Application.ScreenUpdating
    prevCalc = Application.Calculation

    On Error GoTo CleanExit
    
    Application.EnableEvents = False
    Application.ScreenUpdating = False
    Application.Calculation = xlCalculationManual
    
    Set_MainTable ws

    Dim loMain As ListObject
    Set loMain = ws.ListObjects(MainTable)
    
    If loMain Is Nothing Then GoTo CleanExit
    
    Sync_Deletion_MainTable_To_YearTables ws

    Check_AddButton_Position ws, ws.ListObjects(MainTable)
    
    Set_Visibility_ForSheet ws, MainTable

    If loMain.DataBodyRange Is Nothing Then
        Refresh_Snapshot ws, MainTable
        GoTo CleanExit
    End If

    If IsEmpty(MONTHS) Then
        MONTHS = Array("Ene", "Feb", "Mar", "Abr", "May", "Jun", "Jul", "Ago", "Sep", "Oct", "Nov", "Dic")
    End If

    If Len(SIG_DELIM) = 0 Then SIG_DELIM = ChrW(&H241F)

    Dim idxG As Long, idxF As Long, idxH As Long, idxI As Long, idxJ As Long
    
    idxG = loMain.ListColumns(LBL_col_CUOTAS).Index            ' G
    idxF = idxG - 1                                            ' F
    idxH = idxG + 1                                            ' H
    idxI = loMain.ListColumns(LBL_col_M).Index                 ' I
    idxJ = loMain.ListColumns(LBL_col_A).Index                 ' J
    
    If idxF < 1 Or idxH > loMain.ListColumns.Count Then
        Refresh_Snapshot ws, MainTable
        GoTo CleanExit
    End If

    Dim rngF As Range, rngG As Range, rngI As Range, rngJ As Range, rngWatch As Range
    
    Set rngF = loMain.ListColumns(idxF).DataBodyRange
    Set rngG = loMain.ListColumns(idxG).DataBodyRange
    Set rngI = loMain.ListColumns(idxI).DataBodyRange
    Set rngJ = loMain.ListColumns(idxJ).DataBodyRange
    Set rngWatch = Union(rngF, rngG, rngI, rngJ)

    Dim hit As Range
    
    Set hit = Intersect(Target, rngWatch)
    
    If hit Is Nothing Then
        Refresh_Snapshot ws, MainTable
        Set_Visibility_ForSheet ws, MainTable
        
        GoTo CleanExit
    End If

    Dim c As Range
    
    For Each c In hit.Cells
        Dim rowIdx As Long
        rowIdx = c.Row - loMain.DataBodyRange.Rows(1).Row + 1
        If rowIdx < 1 Then GoTo NextC

        ' - Validate F & G: Only Numbers.
        If Not Intersect(c, Union(rngF, rngG)) Is Nothing Then
            If Len(c.Value) > 0 Then
                If Not Validate_Numeric(c.Value) Then
                    c.ClearContents
                    Clear_Row ws, rowIdx
                    loMain.ListColumns(idxH).DataBodyRange.Cells(rowIdx, 1).ClearContents
                    
                    GoTo NextC
                End If
            End If
        End If

        ' - If User Clears F/G -> Clear Mapped Row and Blank H.
        If Not Intersect(c, Union(rngF, rngG)) Is Nothing Then
            If Len(c.Value) = 0 Then
                Clear_Row ws, rowIdx
                loMain.ListColumns(idxH).DataBodyRange.Cells(rowIdx, 1).ClearContents
                loMain.ListColumns(idxI).DataBodyRange.Cells(rowIdx, 1).ClearContents
                loMain.ListColumns(idxJ).DataBodyRange.Cells(rowIdx, 1).ClearContents
                
                GoTo NextC
            End If
        End If

        ' - Current Values.
        Dim valF As Variant, valG As Variant, valI As String, valJ As Variant
        
        valF = loMain.ListColumns(idxF).DataBodyRange.Cells(rowIdx, 1).Value
        valG = loMain.ListColumns(idxG).DataBodyRange.Cells(rowIdx, 1).Value
        valI = CStr(loMain.ListColumns(idxI).DataBodyRange.Cells(rowIdx, 1).Value)
        valJ = loMain.ListColumns(idxJ).DataBodyRange.Cells(rowIdx, 1).Value

        ' - Build Only When F,G,I,J Are Present and F,G > 0.
        Dim okFG As Boolean
        
        okFG = (Len(valF) > 0 And Len(valG) > 0 And IsNumeric(valF) And IsNumeric(valG) And CDbl(valF) > 0 And CDbl(valG) > 0)

        If okFG And Len(Trim$(valI)) > 0 And Not Check_If_Empty(valJ) Then
            Dim n As Long, startYear As Long, mIdx As Long
            
            n = CLng(valG)
            
            If Not IsNumeric(valJ) Then GoTo SkipBuild
            startYear = CLng(valJ)
            mIdx = Get_Month_Index(valI)
            
            If mIdx = 0 Then GoTo SkipBuild

            ' - Clear Mapped Row Before Placing New Checkboxes.
            Clear_Row ws, rowIdx

            ' - Place Checkboxes and Highlight.
            Place_Checkbox ws, rowIdx, startYear, mIdx, n
        End If

SkipBuild:
        Update_ColumnH loMain, rowIdx, idxF, idxG, idxH
NextC:
    Next c

    Monitor_Protection ws, Target
    Refresh_Snapshot ws, MainTable
    Set_Visibility_ForSheet ws, MainTable

CleanExit:
    Application.Calculation = prevCalc
    Application.ScreenUpdating = prevScreen
    Application.EnableEvents = prevEvents
    Monitor_Protection ws, Target

End Sub
